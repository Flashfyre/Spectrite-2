plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.3'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		url = 'https://maven.terraformersmc.com/releases'
		content {
			includeGroup 'com.terraformersmc'
		}
	}
	maven {
		url = 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		name = 'Ladysnake Mods'
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
		content {
			includeGroup 'io.github.ladysnake'
			includeGroupByRegex 'dev\\.onyxstudios.*'
		}
	}
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
	maven { url "https://maven.shedaniel.me/" }
	maven {
		url = "https://jitpack.io"
		content {
			includeGroupByRegex "com.github.*"
		}
	}
}

loom {
	accessWidenerPath = file("src/main/resources/spectrite.accesswidener")
	runs {
		configureEach {
			vmArg("-Dmixin.debug.export=true")
			vmArg "-Dmixin.debug.export.decompile=false"
			vmArg "-Dmixin.debug.verbose=true"
			vmArg "-Dmixin.dumpTargetOnFailure=true"
			vmArg "-Dmixin.checks=true"
			vmArg "-Dmixin.hotSwap=true"
		}
	}
}

quiltflower {
	addToRuntimeClasspath.set(true)
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "io.github.ladysnake:satin:${satin_version}"
	include "io.github.ladysnake:satin:${satin_version}"

	compileOnly "org.jetbrains:annotations:${jb_annotations_version}"

	implementation("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")
	annotationProcessor("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")

	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.0.1"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.0.1"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-item:5.0.1"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-item:5.0.1"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.0.1"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.0.1"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:5.0.1"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:5.0.1"

	modCompileOnly "software.bernie.geckolib:geckolib-fabric-1.19:${project.geckolib_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:7.0.72") {
		exclude(group: "net.fabricmc.fabric-api")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
