#version 150

uniform sampler2D Sampler0;
uniform vec4 ColorModulator;
uniform float STime;
uniform float Saturation;

in vec2 texCoord;

out vec4 fragColor;

vec3 rgb2hsv(vec3 c)
{
    vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    vec4 p = c.g < c.b ? vec4(c.bg, K.wz) : vec4(c.gb, K.xy);
    vec4 q = c.r < p.x ? vec4(p.xyw, c.r) : vec4(c.r, p.yzx);

    float d = q.x - min(q.w, q.y);
    float e = 1.0e-10;
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
}

vec3 hsv2rgb(vec3 c)
{
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

void main()
{
    vec4 color = texture(Sampler0, texCoord);
    if (color.a == 0.0) discard;
    vec3 hsv = rgb2hsv(color.rgb);
    hsv[0] = mod(hsv[0] + STime * 0.3, 1.0);
    hsv[1] *= Saturation;
    fragColor = vec4(hsv2rgb(hsv), color.a) * ColorModulator;
}